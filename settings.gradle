rootProject.name = 'java-prac'

["sub"].each { topModuleName ->
    File topModuleDir = makeDir(rootDir, topModuleName)

    topModuleDir.listFiles().each { subModuleDir ->
        subModuleDir.listFiles().each {moduleDir ->
            makeGradle(moduleDir)
            makeSrc(moduleDir)

            def projectName = ":${subModuleDir.name}-${moduleDir.name}"
            include projectName
            project(projectName).projectDir = moduleDir
        }
    }
}

private static File makeDir(File parentDri, String moduleName) {
    def moduleDir = new File(parentDri, moduleName)
    if (!moduleDir.exists())
        moduleDir.mkdir()
    moduleDir
}

private static void makeGradle(File moduleDir) {
    def gradleFile = new File(moduleDir, "build.gradle")
    if (!gradleFile.exists()) {
        gradleFile.text =
                """
                dependencies {
                
                
                }
                """.stripIndent()
    }
}

private static void makeSrc(File moduleDir) {
    [
            "src/main/java/com/sandro",
            "src/main/resources",
            "src/test/java/com/sandro",
            "src/test/resources",
    ].each { srcDir ->
        def srcFolder = new File(moduleDir, srcDir)

        if (!srcFolder.exists())
            srcFolder.mkdirs()
    }
}
